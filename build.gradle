import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'io.th0rgal'
version = '1.0.0'

repositories {
    mavenCentral()
    // spigot
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    // bstats
    maven { url "https://repo.codemc.org/repository/maven-public" }
    // protocollib
    maven { url "https://repo.dmulloy2.net/nexus/repository/public/" }
    // command api
    maven { url = "https://jitpack.io" }
    // nbt api (used by command api)
    maven { url = "https://repo.codemc.org/repository/maven-public/" }
    // adventure
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    compileOnly group: "org.spigotmc", name: "spigot", version: "1.17-R0.1-SNAPSHOT"
    compileOnly group: "org.spigotmc", name: "spigot-api", version: "1.17-R0.1-SNAPSHOT"
    compileOnly group: "com.github.dmulloy2", name: "ProtocolLib", version: "4.6.0"
    implementation "org.bstats:bstats-bukkit:2.2.1"
    implementation "io.github.xtomlj:xtomlj:1.1.0"
    implementation 'dev.jorel.CommandAPI:commandapi-shade:6.1.0'
    implementation "net.kyori:adventure-text-minimessage:4.1.0-SNAPSHOT"
    implementation "net.kyori:adventure-platform-bukkit:4.0.0-SNAPSHOT"
    implementation "net.kyori:adventure-text-serializer-plain:4.8.1"
    testImplementation platform('org.junit:junit-bom:5.8.0-SNAPSHOT')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

processResources {
    filesNotMatching(["**.png"]) {
        expand version: version
    }
}

shadowJar {
    relocate "org.bstats", "io.th0rgal.guardian.shaded.bstats"
    relocate "org.tomlj", "io.th0rgal.guardian.shaded.tomlj"
    relocate "net.kyori", "io.th0rgal.guardian.shaded.kyori"
    /*
    minimize {
        exclude("io.th0rgal.guardian.api.*")
    }
    */
    manifest {
        attributes(
                'Built-By': System.properties['user.name'],
                'Version': archiveVersion.get(),
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd' 'HH:mm:ss.SSSZ").format(new Date()),
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

compileJava.dependsOn clean
build.dependsOn shadowJar

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}